<START> ::= <disjunction>
	[Accept]

<disjunction> ::= <disjunction> BOOL_OR <xor-exp>
	[ExtendDisjunction]
<disjunction> ::= <xor-exp>
	[DisjunctionFromXorExp]

<xor-exp> ::= <xor-exp> BOOL_XOR <conjunction>
	[Function]
<xor-exp> ::= <conjunction>
	[XorExpFromConjunction]

<conjunction> ::= <conjunction> BOOL_AND <bool-exp>
	[ExtendConjunction]
<conjunction> ::= <bool-exp>
	[ConjunctionFromBoolExp]

<bool-exp> ::= <value> <predicate> <value>
	[BoolExpFromComparison]
<bool-exp> ::= <bool-function>
	[BoolExpFromBoolFunction]
<bool-exp> ::= BANG <bool-exp>
	[BoolExpFromNegation]
<bool-exp> ::= LPAREN <disjunction> RPAREN
	[CreateNestedBoolExp]

<value> ::= STRING
	[ValueFromString]
<value> ::= <bit-disjunction>
	[ValueFromBitDisjunction]

<predicate> ::= EQ
	[PredicateFromEQ]
<predicate> ::= NEQ
	[Function]
<predicate> ::= LT
	[Function]
<predicate> ::= GT
	[Function]
<predicate> ::= LE
	[PredicateFromLE]
<predicate> ::= GE
	[Function]

<bool-function> ::= EXISTS LPAREN <name> RPAREN
	[CreateExistsFunction]
<bool-function> ::= IS_INT32 LPAREN <name> RPAREN
	[CreateIsInt32Function]
<bool-function> ::= IS_INT64 LPAREN <name> RPAREN
	[Function]
<bool-function> ::= IS_NAN LPAREN <name> RPAREN
	[Function]
<bool-function> ::= IS_STRING LPAREN <name> RPAREN
	[Function]
<bool-function> ::= IS_OPAQUE LPAREN <name> RPAREN
	[Function]
<bool-function> ::= EQUALS LPAREN <name> COMMA <arg-list> RPAREN
	[Function]
<bool-function> ::= CONTAINS LPAREN <name> COMMA <arg-list> RPAREN
	[Function]
<bool-function> ::= BEGINS_WITH LPAREN <name> COMMA <arg-list> RPAREN
	[Function]
<bool-function> ::= ENDS_WITH LPAREN <name> COMMA <arg-list> RPAREN
	[Function]
<bool-function> ::= WILDCARD LPAREN <name> COMMA <arg-list> RPAREN
	[Function]
<bool-function> ::= REGEX LPAREN <name> COMMA <arg-list> RPAREN
	[CreateRegexFunction]

<value-function> ::= DECOMPOSE LPAREN <value> RPAREN
	[CreateDecomposeFunction]
<value-function> ::= DECOMP_COMPAT LPAREN <value> RPAREN
	[Function]
<value-function> ::= PRIMARY LPAREN <value> RPAREN
	[CreatePrimaryFunction]
<value-function> ::= SECONDARY LPAREN <value> RPAREN
	[Function]
<value-function> ::= TERTIARY LPAREN <value> RPAREN
	[Function]

<bit-disjunction> ::= <bit-disjunction> BIT_OR <bit-xor-exp>
	[Function]
<bit-disjunction> ::= <bit-xor-exp>
	[BitDisjunctionFromBitXorExp]

<bit-xor-exp> ::= <bit-xor-exp> BIT_XOR <bit-conjunction>
	[Function]
<bit-xor-exp> ::= <bit-conjunction>
	[BitXorExpFromBitConjunction]

<bit-conjunction> ::= <bit-conjunction> BIT_AND <bit-shift-exp>
	[Function]
<bit-conjunction> ::= <bit-shift-exp>
	[BitConjunctionFromBitShiftExp]

<bit-shift-exp> ::= <bit-shift-exp> BIT_SHL <sum>
	[Function]
<bit-shift-exp> ::= <bit-shift-exp> BIT_SHR <sum>
	[Function]
<bit-shift-exp> ::= <bit-shift-exp> BIT_LSR <sum>
	[Function]
<bit-shift-exp> ::= <sum>
	[BitShiftExpFromSum]

<sum> ::= <sum> PLUS <product>
	[SumFromPlusExp]
<sum> ::= <sum> MINUS <product>
	[SumFromMinusExp]
<sum> ::= <product>
	[SumFromProduct]

<product> ::= <product> TIMES <num-exp>
	[ProductFromTimesExp]
<product> ::= <product> DIV <num-exp>
	[Function]
<product> ::= <product> MOD <num-exp>
	[ProductFromModExp]
<product> ::= <num-exp>
	[ProductFromNumExp]

<num-exp> ::= MINUS <num-value>
	[Function]
<num-exp> ::= NEG <num-value>
	[Function]
<num-exp> ::= <num-value>
	[NumExpFromNumValue]

<num-value> ::= INT32
	[NumValueFromINT32]
<num-value> ::= INT64
	[Function]
<num-value> ::= REAL64
	[Function]
<num-value> ::= <name>
	[NumValueFromName]
<num-value> ::= <value-function>
	[NumValueFromValueFunction]
<num-value> ::= LPAREN <value> RPAREN
	[CreateNestedNumValue]

<name> ::= ID
	[NameFromId]
<name> ::= EQ
	[Function]
<name> ::= NEQ
	[Function]
<name> ::= LT
	[Function]
<name> ::= GT
	[Function]
<name> ::= GE
	[Function]
<name> ::= LE
	[Function]
<name> ::= BOOL_OR
	[Function]
<name> ::= BOOL_XOR
	[Function]
<name> ::= BOOL_AND
	[Function]
<name> ::= BIT_OR
	[Function]
<name> ::= BIT_XOR
	[Function]
<name> ::= BIT_AND
	[Function]
<name> ::= BIT_SHL
	[Function]
<name> ::= BIT_SHR
	[Function]
<name> ::= BIT_LSR
	[Function]
<name> ::= PLUS
	[Function]
<name> ::= MOD
	[Function]
<name> ::= TIMES
	[Function]
<name> ::= DIV
	[Function]
<name> ::= EXISTS
	[Function]
<name> ::= IS_INT32
	[Function]
<name> ::= IS_INT64
	[Function]
<name> ::= IS_NAN
	[Function]
<name> ::= IS_STRING
	[Function]
<name> ::= IS_OPAQUE
	[Function]
<name> ::= EQUALS
	[Function]
<name> ::= CONTAINS
	[Function]
<name> ::= BEGINS_WITH
	[Function]
<name> ::= ENDS_WITH
	[Function]
<name> ::= WILDCARD
	[Function]
<name> ::= REGEX
	[Function]
<name> ::= DECOMPOSE
	[Function]
<name> ::= DECOMP_COMPAT
	[Function]
<name> ::= PRIMARY
	[Function]
<name> ::= SECONDARY
	[Function]
<name> ::= TERTIARY
	[Function]


<arg-list> ::= <arg-list> COMMA <value>
	[Function]
<arg-list> ::= <value>
	[CreateArgList]
