<START> ::= <method> [function]

<method> ::= <mpatt> <temps> <statements> [function]
<method> ::= <mpatt> <temps> [function]
<method> ::= <mpatt> <statements> [function]
<method> ::= <mpatt> [function]

<mpatt> ::= <usel> [function]
<mpatt> ::= <bsel> <vname> [function]
<mpatt> ::= <kpatt> [function]

<temps> ::= vbar <tlist> vbar [function]
<temps> ::= vbar vbar [function]

<statements> ::= <slist> stop <rexp> [function]
<statements> ::= <slist> stop [function]
<statements> ::= <slist> [function]
<statements> ::= <rexp> [function]

<usel> ::= id [function]

<bsel> ::= binop [function]
<bsel> ::= vbar [function]

<vname> ::= id [function]

<kpatt> ::= <kpatt> keyword <vname> [function]
<kpatt> ::= keyword <vname> [function]

<tlist> ::= <tlist> <vname> [function]
<tlist> ::= <vname> [function]

<slist> ::= <slist> stop <exp> [function]
<slist> ::= <exp> [function]

<rexp> ::= caret <exp> stop [function]
<rexp> ::= caret <exp> [function]

<exp> ::= <vname> assign <exp> [function]
<exp> ::= <primary> [function]
<exp> ::= <mexp> [function]
<exp> ::= <cexp> [function]

<primary> ::= <vname> [function]
<primary> ::= <literal> [function]
<primary> ::= <block> [function]
<primary> ::= lparen <exp> rparen [function]

<mexp> ::= <uexp> [function]
<mexp> ::= <bexp> [function]
<mexp> ::= <kexp> [function]

<cexp> ::= <mexp> <cascade> [function]

<literal> ::= num [function]
<literal> ::= char [function]
<literal> ::= string [function]
<literal> ::= hid [function]
<literal> ::= hbinop [function]
<literal> ::= hkeywords [function]
<literal> ::= <aconst> [function]

<block> ::= lbracket <bbody> rbracket [function]
<block> ::= lbracket rbracket [function]

<uexp> ::= <udesc> <usel> [function]

<bexp> ::= <bdesc> <bsel> <udesc> [function]

<kexp> ::= <bdesc> <klist> [function]

<cascade> ::= <cascade> semi <usel> [function]
<cascade> ::= <cascade> semi <bsel> <udesc> [function]
<cascade> ::= <cascade> semi <klist> [function]
<cascade> ::= semi <usel> [function]
<cascade> ::= semi <bsel> <udesc> [function]
<cascade> ::= semi <klist> [function]

<aconst> ::= hlparen <abody> rparen [function]
<aconst> ::= hlparen rparen [function]

<udesc> ::= <primary> [function]
<udesc> ::= <uexp> [function]

<bdesc> ::= <udesc> [function]
<bdesc> ::= <bexp> [function]

<klist> ::= <klist> keyword <bdesc> [function]
<klist> ::= keyword <bdesc> [function]

<abody> ::= <abody> <symbol> [function]
<abody> ::= <abody> <literal> [function]
<abody> ::= <abody> <array> [function]
<abody> ::= <symbol> [function]
<abody> ::= <literal> [function]
<abody> ::= <array> [function]

<symbol> ::= id [function]
<symbol> ::= bsel [function]
<symbol> ::= keywords [function]

<array> ::= lparen <abody> rparen [function]
<array> ::= lparen rparen [function]

<bbody> ::= <alist> vbar <temps> <statements> [function]
<bbody> ::= <alist> vbar <temps> [function]
<bbody> ::= <alist> vbar <statements> [function]
<bbody> ::= <alist> vbar [function]
<bbody> ::= <temps> <statements> [function]
<bbody> ::= <temps> [function]
<bbody> ::= <statements> [function]

<alist> ::= <alist> arg [function]
<alist> ::= arg [function]
