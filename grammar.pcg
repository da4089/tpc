# Copyright (C) 1999-2006 by Mantara Software (ABN 17 105 665 594).
#
# This file is part of tpc.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
#

#
# This is the grammar for the parser compiler itself written in a
# rather self-referential format.
#
# A grammar is a list of productions rules, which in turn consist of
# a nonterminal symbol ("<foo>" for example) followed by the standard
# BNF symbol "::=" and then the list of things which are generated by
# that production, terminals and nonterminals in order.
# All of which is followed by a function name in square brackets which
# is to be called by the parser when it reduces the right-hand side of
# a  production to the left-hand side.
#

<grammar> ::= <production-list>
	[accept_grammar]

<production-list> ::= <production-list> <production>
	[extend_production_list]
<production-list> ::= <production>
	[make_production_list]

<production> ::= <nonterminal> DERIVES <exp-list> <reduction>
	[make_production]

<exp-list> ::= <exp-list> <nonterminal>
	[extend_exp_list]
<exp-list> ::= <exp-list> <terminal>
	[extend_exp_list]
<exp-list> ::= <nonterminal>
	[make_exp_list]
<exp-list> ::= <terminal>
	[make_exp_list]

<nonterminal> ::= LT ID GT
	[make_nonterminal]

<terminal> ::= ID
	[make_terminal]

<reduction> ::= LBRACKET ID RBRACKET
	[make_reduction]
