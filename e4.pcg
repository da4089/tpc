<sub-exp> ::= <or-exp-list> .

<or-exp-list> ::= <or-exp-list> BOOL_OR <xor-exp-list> .
<or-exp-list> ::= <xor-exp-list> .

<xor-exp-list> ::= <xor-exp-list> BOOL_XOR <and-exp-list> .
<xor-exp-list> ::= <and-exp-list> .

<and-exp-list> ::= <and-exp-list> BOOL_AND <bool-exp> .
<and-exp-list> ::= <bool-exp> .

<bool-exp> ::= <value> .
<bool-exp> ::= <value> <predicate> <value> .
<bool-exp> ::= BANG <bool-exp> .

<value> ::= STRING .
<value> ::= <bit-or-exp-list> .

<predicate> ::= EQ .
<predicate> ::= NEQ .
<predicate> ::= LT .
<predicate> ::= GT .
<predicate> ::= LE .
<predicate> ::= GE .

<bit-or-exp-list> ::= <bit-or-exp-list> BIT_OR <bit-xor-exp-list> .
<bit-or-exp-list> ::= <bit-xor-exp-list> .

<bit-xor-exp-list> ::= <bit-xor-exp-list> BIT_XOR <bit-and-exp-list> .
<bit-xor-exp-list> ::= <bit-and-exp-list> .

<bit-and-exp-list> ::= <bit-and-exp-list> BIT_AND <bit-shift-exp-list> .
<bit-and-exp-list> ::= <bit-shift-exp-list> .

<bit-shift-exp-list> ::= <bit-shift-exp-list> BIT_SHL <sum-list> .
<bit-shift-exp-list> ::= <bit-shift-exp-list> BIT_SHR <sum-list> .
<bit-shift-exp-list> ::= <bit-shift-exp-list> BIT_LSR <sum-list> .
<bit-shift-exp-list> ::= <sum-list> .

<sum-list> ::= <sum-list> PLUS <product-list> .
<sum-list> ::= <sum-list> MINUS <product-list> .
<sum-list> ::= <product-list> .

<product-list> ::= <product-list> TIMES <num-exp> .
<product-list> ::= <product-list> DIV <num-exp> .
<product-list> ::= <product-list> MOD <num-exp> .
<product-list> ::= <num-exp> .

<num-exp> ::= <num-value> .
<num-exp> ::= MINUS <num-value> .
<num-exp> ::= NEG <num-value> .

<num-value> ::= INT32 .
<num-value> ::= INT64 .
<num-value> ::= REAL64 .
<num-value> ::= <name> .
<num-value> ::= <function> .
<num-value> ::= LPAREN <bit-or-exp-list> RPAREN .

<name> ::= ID .
<name> ::= EQ .
<name> ::= NEQ .
<name> ::= LT .
<name> ::= GT .
<name> ::= GE .
<name> ::= LE .
<name> ::= BOOL_OR .
<name> ::= BOOL_XOR .
<name> ::= BOOL_AND .
<name> ::= BIT_OR .
<name> ::= BIT_XOR .
<name> ::= BIT_AND .
<name> ::= BIT_SHL .
<name> ::= BIT_SHR .
<name> ::= BIT_LSR .
<name> ::= PLUS .
<name> ::= MOD .
<name> ::= TIMES .
<name> ::= DIV .

<function> ::= <function-name> LPAREN RPAREN .
<function> ::= <function-name> LPAREN <arg-list> RPAREN .

<function-name> ::= ID .

<arg-list> ::= <arg-list> COMMA <value> .
<arg-list> ::= <value> .
